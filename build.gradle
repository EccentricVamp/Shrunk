plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.+'
}

java.toolchain.languageVersion = JavaLanguageVersion.of java_version

version = "${minecraft_version}-${mod_version}"
group = mod_group
archivesBaseName = mod_id

// def mixinProperties = [
//     'mixin.env.remapRefMap'        : 'true',
//     'mixin.env.refMapRemappingFile': file('build/createSrgToMcp/output.srg')
// ]

def baseMods = {
    "${mod_id}" {
        source sourceSets.main
    }
}

minecraft {
    mappings channel: 'official', version: minecraft_version
    runs {
        client {
            taskName "Client"
            workingDirectory file('run')
            // properties mixinProperties
            mods baseMods
        }

        server {
            taskName "Server"
            workingDirectory file('run')
            // properties mixinProperties
            mods baseMods
        }
    }
}

repositories {
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("com.github.Virtuoel:Pehkui:${pehkui_version}-${minecraft_version}-forge")
}

jar.manifest.attributes = [
    'Specification-Title'     : mod_id,
    'Specification-Vendor'    : mod_author,
    'Specification-Version'   : '1',
    'Implementation-Title'    : project.name,
    'Implementation-Version'  : mod_version,
    'Implementation-Vendor'   : mod_author,
    'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ'),
]

jar.finalizedBy 'reobfJar'

curseforge {
    apiKey = System.getenv().CURSEFORGE_KEY ?: 'none'
    project {
        id = curseforge_project_id
        releaseType = curseforge_release_type
        addGameVersion 'Forge'
        addGameVersion minecraft_version
        addGameVersion "Java ${java_version}"
        mainArtifact jar
    }
}

modrinth {
    token = System.getenv().MODRINTH_TOKEN ?: 'none'
    projectId = modrinth_project_id
    versionType = modrinth_release_type
    gameVersions = [minecraft_version]
    uploadFile = jar
}
