plugins {
    id 'dev.architectury.loom' version '0.12.0-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.+'
}

sourceCompatibility = targetCompatibility = JavaLanguageVersion.of project.java_version

archivesBaseName = project.mod_id
version = "${project.minecraft_version}-${project.mod_version}"
group = project.mod_group

loom {
    silentMojangMappingsLicense()
}

repositories {
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.officialMojangMappings()

    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    modImplementation "com.github.Virtuoel:Pehkui:${pehkui_version}-${minecraft_version}-forge"
}

processResources {
    include 'META-INF/mods.toml'
    expand project.properties
}

jar.manifest.attributes = [
    'Specification-Title'     : project.mod_id,
    'Specification-Vendor'    : project.mod_author,
    'Specification-Version'   : '1',
    'Implementation-Title'    : project.name,
    'Implementation-Version'  : project.mod_version,
    'Implementation-Vendor'   : project.mod_author,
    'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ'),
]

modrinth {
    token = System.getenv().MODRINTH_TOKEN ?: 'none'
    projectId = project.modrinth_project_id
    versionName = project.mod_version
    versionNumber = project.version
    versionType = project.modrinth_version_type
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ['forge']
    dependencies {
        required.project 'pehkui'
    }
}
